# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Konsta Kokkinen <kray@tsundere.fi>

pkgname='minetest-server'
pkgver=0.4.15
pkgrel=1
arch=('i686' 'x86_64')
url='http://minetest.net/'
license=('GPL')
pkgdesc='Server of infinite-world block sandbox game'
depends=('minetest-common' 'leveldb' 'curl' 'sqlite' 'hiredis' 'luajit')
install=minetest-server.install
makedepends=('sqlite' 'freetype2' 'leveldb' 'openal' 'libvorbis' 'curl' 'irrlicht'
             'hicolor-icon-theme' 'cmake' 'hiredis' 'luajit')
source=($pkgname-$pkgver.tar.gz::https://github.com/minetest/minetest/archive/$pkgver.tar.gz
        $pkgname-data-$pkgver.tar.gz::https://github.com/minetest/minetest_game/archive/$pkgver.tar.gz
        0001-Add-support-for-unix-socket-connection-to-redis.patch
        minetest@.service)
md5sums=('ff06c4d29bd152b173d346957e715ba1'
         '514fa9bc2b6d1afff2a2524ad8152c7b'
         '43a7d80f6d0658c4e1f7415e1fe40d6a'
         'ec193b09eb85f2518aaa17506ad06c57')

prepare() {
  cd minetest-$pkgver
  patch -Np1 -i "${srcdir}/0001-Add-support-for-unix-socket-connection-to-redis.patch"
}

build() {
  cd minetest-$pkgver
  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_CLIENT=0 \
    -DBUILD_SERVER=1 \
    -DENABLE_LEVELDB=1 \
    -DENABLE_REDIS=1
  make
}

package() {
  cd minetest-$pkgver
  make DESTDIR="$pkgdir" install
  install -d  "$pkgdir"/etc/minetest
  install -Dm644 ../minetest@.service \
    "$pkgdir"/usr/lib/systemd/system/minetest@.service

  rm -rf "$pkgdir"/usr/share/{minetest,appdata,applications,icons,doc}
  rm "$pkgdir"/usr/share/man/man6/minetest.6
}
